* Copyright 2025 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2025-07-27
* https://github.com/Aleksandr3Bocharov/RefalABEinshteinPuzzle

*====================================================================
* RefalABEinshteinPuzzle (GUI версия)
*
* Программа "RefalABEinshteinPuzzle" написана
* на языке программирования РефалАБ.
* https://github.com/Aleksandr3Bocharov/RefalAB
*
* Решение задачи Эйнштейна на РефалАБ.
* 
* Задача Эйнштейна.
* 
* Условия:
* 1. Есть 5 домов разных цветов.
* 2. В каждом доме живет по одному человеку,
* отличающегося от другого по национальности.
* 3. Каждый обитатель пьет только один
* определенный напиток, курит определенную
* марку сигарет и держит животное.
* 4. Никто из пяти людей не пьет
* одинакового напитка, не курит одинаковые
* сигареты и не держит одинаковых животных.
* 
* Вопрос: Чья рыба?
* 
* Подсказки:
* 1.  Англичанин живет в красном доме.
* 2.  У шведа есть собака.
* 3.  Датчанин пьет чай.
* 4.  Зеленый дом стоит слева от белого.
* 5.  Обитатель зеленого дома пьет кофе.
* 6.  Человек, который курит Pall Mall, держит птицу.
* 7.  Обитатель среднего дома пьет молоко.
* 8.  Обитатель желтого дома курит Dunhill.
* 9.  Норвежец живет в первом доме.
* 10. Курильщик Marlboro живет возле того,
*     у кого есть кошка.
* 11. Человек, у которого есть конь,
*     живет возле того, кто курит Dunhill.
* 12. Курильщик Winfield пьет пиво.
* 13. Норвежец живет возле голубого дома.
* 14. Немец курит Rothmans.
* 15. Курильщик Marlboro живет по соседству 
*     с человеком, который пьет воду.
*====================================================================

RefalABEinshteinPuzzle_GUI +
    START

    ENTRY Go
    EXTRN Answer, Solve
    EXTRN Map_Accumulator, Del_Accumulator, P1, Symb, Br, Dg
    EXTRN True, False
    EXTRN GUI_Init, GUI_Close, Dialog_Info, Dialog_Is_Exit
    EXTRN View_Answer, View_Rule, View_Table, View_Show
    EXTRN Steps, Steps_Back, Home
    EXTRN Color, Yellow, Blue, Red, Green, White
    EXTRN Nationality, Norwegian, Dane, Englishman, German, Swede
    EXTRN Cigarettes, Dunhill, Rothmans, PallMall, Marlboro, Winfield
    EXTRN Pet, Cat, Horse, Bird, Dog, Fish
    EXTRN Drink, Water, Tea, Milk, Coffee, Beer

    IMPL

Go      = <GUI_Init> <Is_Info <Dialog_Info>> <GUI_Close>

Is_Info
    &False +
        = <Is_Info_Aux <Dialog_Is_Exit>>
    &True +
        = <Br &Steps '=' 0> <Br &Steps_Back '=' 0> +
        <Solve_Out <Solve (0 (1 2 3 4 5 6 7 8 9 10 11 12 13 14 15) '=' +
            (&Home 1 &Color '???' &Nationality '???' +
            &Cigarettes '???' &Pet '???' &Drink '???') +
            (&Home 2 &Color '???' &Nationality '???' +
            &Cigarettes '???' &Pet '???' &Drink '???') +
            (&Home 3 &Color '???' &Nationality '???' +
            &Cigarettes '???' &Pet '???' &Drink '???') +
            (&Home 4 &Color '???' &Nationality '???' +
            &Cigarettes '???' &Pet '???' &Drink '???') +
            (&Home 5 &Color '???' &Nationality '???' +
            &Cigarettes '???' &Pet '???' &Drink '???'))>>

Is_Info_Aux
    &True +
        =
    &False +
        = <Is_Info <Dialog_Info>>

Solve_Out
    E.Solve_B (E._ '=' E.Table) +
        = <View_Answer <Dg &Steps> <Dg &Steps_Back> <Answer_Text <Answer E.Table>>> +
        <Do_Solve_Out E.Solve_B ((E._ '=' E.Table))>

Answer_Text
    &Norwegian +
        = 'Норвежец'
    &Dane +
        = 'Датчанин'
    &Englishman +
        = 'Англичанин'
    &German +
        = 'Немец'
    &Swede +
        = 'Швед'

Do_Solve_Out
    (W.Solve_B) E.Solve_E +
        = <Do_Solve_Out_Aux W.Solve_B 0> +
        <Result_View (W.Solve_B) E.Solve_E <View_Show>>
    E.Solve_B (W.Solve_E) +
        = <Do_Solve_Out_Aux W.Solve_E 2> +
        <Result_View E.Solve_B (W.Solve_E) <View_Show>>
    E.Solve_B (W.Solve_M) E.Solve_E +
        = <Do_Solve_Out_Aux W.Solve_M 1> +
        <Result_View E.Solve_B (W.Solve_M) E.Solve_E <View_Show>>

Do_Solve_Out_Aux
    (S.Rule E._ '=' V.Table) S.Position +
        = <View_Rule S.Position S.Rule <Rule_Text S.Rule>> +
        <Del_Accumulator <Map_Accumulator &Do_Map_Row 1 V.Table>>

Do_Map_Row
    S.Row (&Home S.Home &Color E.Color &Nationality E.Nationality +
    &Cigarettes E.Cigarettes &Pet E.Pet &Drink E.Drink) +
        = <P1 S.Row> +
        <Del_Accumulator <Map_Accumulator &Do_Map_Col (S.Row 0) +
            (<Symb S.Home>) +
            (<Object_Text E.Color>) +
            (<Object_Text E.Nationality>) +
            (<Object_Text E.Cigarettes>) +
            (<Object_Text E.Pet>) +
            (<Object_Text E.Drink>)>>

Do_Map_Col
    (S.Row S.Column) (E.Object_Text) +
        = (S.Row <P1 S.Column>) <View_Table S.Row S.Column E.Object_Text>

* <Rule_Text S(0..16).Rule> == V.Rule_txt
*
* V.Rule_txt ::= 'Условия.' |
*                'Англичанин живет в красном доме.' | 
*                'У шведа есть собака.' |
*                'Датчанин пьет чай.' |
*                'Зеленый дом стоит слева от белого.' |
*                'Обитатель зеленого дома пьет кофе.' |
*                'Человек, который курит Pall Mall,
*                 держит птицу.' |
*                'Обитатель среднего дома пьет молоко.' |
*                'Обитатель желтого дома курит Dunhill.' |
*                'Норвежец живет в первом доме.' |
*                'Курильщик Marlboro живет возле того,
*                 у кого есть кошка.' |
*                'Человек, у которого есть конь,
*                 живет возле того, кто курит Dunhill.' |
*                'Курильщик Winfield пьет пиво.' |
*                'Норвежец живет возле голубого дома.' |
*                'Немец курит Rothmans.' | 
*                'Курильщик Marlboro живет по соседству
*                 с человеком, который пьет воду.' | 
*                'Рыба - пятое животное.'
Rule_Text
    0   = 'Условия.'
    1   = 'Англичанин живет в красном доме.'
    2   = 'У шведа есть собака.'
    3   = 'Датчанин пьет чай.'
    4   = 'Зеленый дом стоит слева от белого.'
    5   = 'Обитатель зеленого дома пьет кофе.'
    6   = 'Человек, который курит Pall Mall, ' +
        'держит птицу.'
    7   = 'Обитатель среднего дома пьет молоко.'
    8   = 'Обитатель желтого дома курит Dunhill.'
    9   = 'Норвежец живет в первом доме.'
    10  = 'Курильщик Marlboro живет возле ' +
        'того, у кого есть кошка.'
    11  = 'Человек, у которого есть конь, ' +
        'живет возле того, кто курит Dunhill.'
    12  = 'Курильщик Winfield пьет пиво.'
    13  = 'Норвежец живет возле голубого дома.'
    14  = 'Немец курит Rothmans.'
    15  = 'Курильщик Marlboro живет по соседству ' +
        'с человеком, который пьет воду.'
    16  = 'Рыба - пятое животное.'

* <Object_Text V.Object> == V.Object_txt
*
* V.Object ::= '???' |
*              &Yellow | &Blue | &Red | &Green | &White |
*              &Norwegian | &Dane | &Englishman | &German | &Swede |
*              &Dunhill | &Rothmans | &PallMall | &Marlboro | &Winfield |
*              &Cat | &Horse | &Bird | &Dog | &Fish |
*              &Water | &Tea | &Milk | &Coffee | &Beer
* V.Object_txt ::= '???' |
*                  'Желтый' | 'Голубой' | 'Красный' | 'Зеленый' | 
*                  'Белый' |
*                  'Норвежец' | 'Датчанин' | 'Англичанин' |
*                  'Немец' | 'Швед' |
*                  'Dunhill' | 'Rothmans' | 'Pall Mall' | 'Marlboro' | 'Winfield' |
*                  'Кошка' | 'Конь' | 'Птица' | 'Собака' | 'Рыба' |
*                  'Вода' | 'Чай' | 'Молоко' | 'Кофе' | 'Пиво'
Object_Text
    '???' +
        = '???'
    &Yellow +
        = 'Желтый'
    &Blue +
        = 'Голубой'
    &Red +
        = 'Красный'
    &Green +
        = 'Зеленый'
    &White +
        = 'Белый'     
    &Norwegian +
        = 'Норвежец' 
    &Dane +
        = 'Датчанин'  
    &Englishman +
        = 'Англичанин'
    &German +
        = 'Немец'
    &Swede +
        = 'Швед'   
    &Dunhill +
        = 'Dunhill'
    &Rothmans +
        = 'Rothmans'
    &PallMall +
        = 'Pall Mall'
    &Marlboro +
        = 'Marlboro' 
    &Winfield +
        = 'Winfield'  
    &Cat +
        = 'Кошка'
    &Horse +
        = 'Конь'
    &Bird +
        = 'Птица'
    &Dog +
        = 'Собака'
    &Fish +
        = 'Рыба'
    &Water +
        = 'Вода'
    &Tea +
        = 'Чай'
    &Milk +
        = 'Молоко'
    &Coffee +
        = 'Кофе'
    &Beer +
        = 'Пиво'

Result_View
    E._ 'Q' +
        = <Result_View_Aux E._ <Dialog_Is_Exit>>
    W.Solve_B E.Solve_M (W.Solve_C) E.Solve_E 'B' +
        = <Do_Solve_Out (W.Solve_B) E.Solve_M W.Solve_C E.Solve_E>
    E.Solve_B W.Solve_P (W.Solve_C) E.Solve_E 'P' +
        = <Do_Solve_Out E.Solve_B (W.Solve_P) W.Solve_C E.Solve_E>
    E.Solve_B (W.Solve_C) W.Solve_N E.Solve_E 'N' +
        = <Do_Solve_Out E.Solve_B W.Solve_C (W.Solve_N) E.Solve_E>
    E.Solve_B (W.Solve_C) E.Solve_M W.Solve_E 'E' +
        = <Do_Solve_Out E.Solve_B W.Solve_C E.Solve_M (W.Solve_E)>

Result_View_Aux
    E._ &True +
        =
    E._ &False +
        = <Result_View E._ <View_Show>>

    END
